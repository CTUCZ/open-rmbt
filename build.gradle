import org.gradle.api.artifacts.*

group 'at.alladin.rmbt'
version '1.0-SNAPSHOT'
apply plugin: 'base' // To add "clean" task to the root project.

subprojects {
    apply from: rootProject.file('common.gradle')
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
    }
}

def getVersionNameFromGit() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getBranchFromGit() {
    def stdoutBranch = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'symbolic-ref', '--short', 'HEAD'
        standardOutput = stdoutBranch
    }
    return stdoutBranch.toString().trim()
}

def getHashFromGit() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "log", "--pretty=format:'%h'", "-n 1"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getVersionCodeFromGit() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', 'HEAD', '--count'
        standardOutput = stdout
    }
    return stdout.toString().trim().toInteger()
}